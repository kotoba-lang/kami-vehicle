(ns vehicle.wheel-test
  "Ported 1:1 from kami-vehicle's `src/wheel.rs` `#[cfg(test)] mod tests`."
  (:require [clojure.test :refer [deftest is]]
            [vehicle.wheel :as wheel]
            [vehicle.vec3 :as v3]))

(deftest pacejka-zero-slip-zero-force
  (let [p (wheel/pacejka-road-dry)
        f (wheel/pacejka-force p {:fz 4000.0 :vx 20.0 :vy 0.0 :vs 20.0})]
    (is (< (Math/abs (:fx f)) 5.0))
    (is (< (Math/abs (:fy f)) 5.0))))

(deftest pacejka-locked-wheel-pushes-back
  (let [p (wheel/pacejka-road-dry)
        f (wheel/pacejka-force p {:fz 4000.0 :vx 20.0 :vy 0.0 :vs 0.0})]
    (is (< (:fx f) -1000.0))))

(deftest pacejka-friction-circle-clamps-combined-force
  (let [p (wheel/pacejka-road-dry)
        f (wheel/pacejka-force p {:fz 4000.0 :vx 5.0 :vy 8.0 :vs 0.0})
        total (Math/sqrt (+ (* (:fx f) (:fx f)) (* (:fy f) (:fy f))))]
    (is (<= total (* 4000.0 1.05)))))

(deftest wheel-steer-clamps
  (let [w (assoc (wheel/new-wheel 0 0 1 0.32 0.22) :max-steer-angle 0.4)
        w (wheel/set-steer w 1.0)]
    (is (< (Math/abs (- (:steer-angle w) 0.4)) 1e-6))
    (let [w (wheel/set-steer w -1.0)]
      (is (< (Math/abs (+ (:steer-angle w) 0.4)) 1e-6)))))

(deftest wheel-frame-with-zero-steer-returns-chassis-basis
  (let [[f l u] (wheel/wheel-frame v3/unit-x v3/unit-y 0.0)]
    (is (< (v3/length (v3/sub f v3/unit-x)) 1e-5))
    (is (< (v3/length (v3/sub u v3/unit-y)) 1e-5))
    (is (< (v3/length (v3/sub l v3/neg-unit-z)) 1e-5))))
